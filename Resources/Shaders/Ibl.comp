#version 450
#extension GL_ARB_separate_shader_objects : enable

layout(local_size_x = WORKGROUP_SIZE, local_size_y = WORKGROUP_SIZE, local_size_z = 1) in;

layout(rgba16f, set = 0, binding = 0) uniform writeonly imageCube outColour;

layout(set = 0, binding = 1) uniform samplerCube samplerColour;

#include "Shaders/Lighting.glsl"

const float roughness = 0.6f;

void main()
{
	if (gl_GlobalInvocationID.x >= WIDTH || gl_GlobalInvocationID.y >= HEIGHT)
	{
		return;
	}

	vec2 uv = (gl_GlobalInvocationID.xy + vec2(0.5f, 0.5f)) / vec2(WIDTH, HEIGHT);

	for (uint i = 0; i != 6; ++i)
	{
		vec3 localDir = normalize(cube_dir(uv, i));
		vec3 conv = vec3(0.0f);

		if (roughness <= 0.0f)
		{
			conv = texture(samplerColour, localDir).rgb;
		}
		else if (roughness >= 1.0f)
		{
			conv = diffuse_convolution(samplerColour, localDir);
		}
		else
		{
			conv = specular_convolution(samplerColour, localDir, roughness);
		}

		imageStore(outColour, ivec3(gl_GlobalInvocationID.xy, i), vec4(conv, 1.0f));
	}
}
